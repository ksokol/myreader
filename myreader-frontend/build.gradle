plugins {
  id "com.github.node-gradle.node" version "2.2.4"
}

apply plugin: 'base'

idea.module {
  excludeDirs += [
    file('build'),
    file('dist'),
    file('node_modules')
  ]
}

node {
  version = '14.15.3'
  npmVersion = '6.14.10'
  download = true
}

task dependencyCheckAnalyze(type: NpmTask, dependsOn: npmInstall) {
  args = ['run', 'audit:high']
}

task lint(type: NpmTask, dependsOn: npmInstall) {
  args = ['run', 'lint']
}

task test(dependsOn: [npmInstall, npm_test])

check.dependsOn = [lint, test]

task build(type: NpmTask, dependsOn: [npmInstall, test], overwrite: true) {
  inputs.files files('src', 'webpack.config.js', '.babelrc')
  outputs.dir file('dist')

  def commitId = 'git rev-parse --verify --short HEAD'.execute().text.trim()
  args = ['run', 'build', '--', '--env', "version=$project.version", '--env', "commitId=$commitId"]
}

clean {
  delete 'dist'
}

task devRun(type: NpmTask) {
  args = ['start']
}

plugins {
    id 'com.moowork.node' version '1.2.0'
}

idea.module {
    excludeDirs += [
        file('build'),
        file('dist')
    ]
}

node {
    version = '10.16.0'
    npmVersion = '6.9.0'
    download = true
}

task resolve(dependsOn: npm_install) {
    doLast {
        resolveDependencies(project)
    }
}

// Workaround for https://github.com/srs/gradle-node-plugin/issues/131
project.afterEvaluate {
    tasks.npm_install.inputs.file(file('package.json'))
    tasks.npm_install.outputs.dir(file('node_modules'))
    tasks.npm_install.outputs.file(file('package-lock.json'))
}

task dependencyCheckAnalyze(type: NpmTask, dependsOn: npm_install) {
    args = ['audit']
}

task lint(type: NpmTask, dependsOn: npm_install) {
    args = ['run', 'lint']
}

task test(dependsOn: [npm_install, npm_test]) {
    inputs.files files('src', 'webpack.config.js', 'package.json')
}

task build(type: NpmTask, dependsOn: [npm_install, test]) {
    outputs.files file('dist')
    inputs.files files('src')

    args = ['run', 'build']
}

task clean() {
    delete "dist"
}

task check(dependsOn: [lint, test])

task devRun(type: NpmTask) {
    args = ['start']
}

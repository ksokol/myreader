plugins {
    id 'com.moowork.node' version '1.1.1'
    id 'com.wiredforcode.spawn' version '0.8.2'
}

apply plugin: 'com.moowork.node'
apply plugin: 'idea'

node {
    version = '10.15.0'
    npmVersion = '6.4.1'
    download = true
}

idea {
    module {
        sourceDirs += file('src/app/js')
    }
}

task resolve(dependsOn: npm_install) {
    doLast {
        resolveDependencies(project)
    }
}

task dependencyCheckAnalyze(type: NpmTask, dependsOn: npm_install) {
    args = ['audit']
}

task lint(type: NpmTask, dependsOn: npm_install) {
    args = ['run', 'lint']
}

task test(overwrite: true, dependsOn: [npm_install, npm_test]) {
    inputs.files files('src', 'test', 'webpack.config.js', 'src/tests.webpack.js', 'package.json', 'karma.conf.js')
}

task build(type: NpmTask, dependsOn: [npm_install, test]) {
    outputs.files file('dist')
    inputs.files files('src')

    args = ['run', 'build']
}

task clean() {
    delete "dist"
}

task check(dependsOn: [lint, test])

task startServer(type: SpawnProcessTask) {
    command 'npm start'
    ready 'webpack: Compiled successfully.'
    directory = "${projectDir}"
    pidLockFileName 'build/frontend.lock'
}

task stopServer(type: KillProcessTask) {
    pidLockFileName 'build/frontend.lock'
    directory = "${projectDir}"
}

sonarqube {
    properties {
        property "sonar.sources", "${projectDir}/src/app/js"
        property "sonar.exclusions", ["**/*.spec.js", "**/test-utils.js"]
        property "sonar.tests", ["${projectDir}/src/app/js"]
        property "sonar.test.inclusions", ["**/*.spec.js"]
        property "sonar.javascript.lcov.reportPaths", "${buildDir}/reports/coverage/lcov.info"
        property "sonar.testExecutionReportPaths", "${buildDir}/TESTS.xml"
        property "sonar.eslint.reportPaths", "${buildDir}/reports/eslint.json"
    }
}
